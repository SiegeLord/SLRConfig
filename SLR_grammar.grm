Comment Line = '#'

"Start Symbol" = <TableContents>

! -------------------------------------------------
! Character Sets
! -------------------------------------------------

{Raw String Chars} = {Printable} + {Whitespace}
{Raw String 0 Chars} = {Printable} + {Whitespace} - ["]

{String Middle} = {Printable} - {Whitespace} - [#='['']'{}$",~] + [' ']
{String Border} = {String Middle} - [' ']

! -------------------------------------------------
! Terminals
! -------------------------------------------------

EscapedString = ({String Border} | '\' {Raw String Chars}) ({String Middle} | '\' {Raw String Chars})* ({String Border} | '\' {Raw String Chars})
                | ({String Border} | '\' {Raw String Chars})

RawString0 = '"' {Raw String 0 Chars}* '"'
RawString1 = '{{"' {Raw String Chars}* '"}}'
RawString2 = '{{{"' {Raw String Chars}* '"}}}'

! -------------------------------------------------
! Rules
! -------------------------------------------------

<OptComma> ::= ','
            |

! Expressions
<String> ::= EscapedString | RawString0 | RawString1 | RawString2

<Expansion> ::= '$' <String>

<Expr> ::= <String>
       |   <Expansion>
       |   <Expr> '~' <String>
       |   <Expr> '~' <Expansion>

! Array

<Array> ::= '[' <ArrayContents> ']'

<ArrayElement> ::= <Expr>
                | <Table>
                | <Array>

<ArrayElements> ::= <ArrayElement>
                 |  <ArrayElements> ',' <ArrayElement>

<ArrayContents> ::= <ArrayElements> <OptComma>
                     |
! Table

<Table> ::= '{' <TableContents> '}'

<TableElement> ::= <String> <Table>
                |  <String> '=' <Array>
                |  <String> '=' <Expr>

<TableElements> ::= <TableElement>
                 |  <TableElements> <OptComma> <TableElement>

<TableContents> ::= <TableElements> <OptComma>
                     |

