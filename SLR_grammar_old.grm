Comment Line = '#'

"Start Symbol" = <TableContents>

! -------------------------------------------------
! Character Sets
! -------------------------------------------------
                      
{Raw String Chars} = {Printable} + {Whitespace}
{Raw String 0 Chars} = {Printable} + {Whitespace} - ["]

{String Middle} = {Printable} - {Whitespace} - [#='['']'{}$",~] + [' ']
{String Border} = {String Middle} - [' ']

! -------------------------------------------------
! Terminals
! -------------------------------------------------

EscapedString = ({String Border} | '\' {Raw String Chars}) ({String Middle} | '\' {Raw String Chars})* ({String Border} | '\' {Raw String Chars})
                | ({String Border} | '\' {Raw String Chars})

RawString0 = '"' {Raw String 0 Chars}* '"'
RawString1 = '{{"' {Raw String Chars}* '"}}'
RawString2 = '{{{"' {Raw String Chars}* '"}}}'

! -------------------------------------------------
! Rules
! -------------------------------------------------

<OptComma> ::= ','
            |

! Expressions
<String> ::= EscapedString | RawString0 | RawString1 | RawString2
          
<IndexExpr> ::= <String>
             |  <IndexExpr> '[' <String> ']'
             | 'root'
             | 'import'

<Expansion> ::= '$' <IndexExpr>

<StringSource> ::= <String>
                |  <Expansion>

<StringExpr> ::= <StringSource>
             |   <StringExpr> '~' <StringSource>

<NoDeleteExpr> ::= <StringExpr>        
                |  '{' <TableContents> '}'
                |  '[' <ArrayContents> ']'

<Expr> ::= <NoDeleteExpr> |  'delete'

! Statements
<AssignIndex> ::= <String>
              |  <AssignIndex> '[' <String> ']'

<Assignment> ::= <AssignIndex> '=' <Expr>

<ArrayElement> ::= <Assignment> | <NoDeleteExpr>
             
<ArrayElements> ::= <ArrayElement>
                 |  <ArrayElements> ',' <ArrayElement>

<ArrayContents> ::= <ArrayElements> <OptComma>
                     |

<TableElement> ::= <Assignment> | <Expansion>

<TableElements> ::= <TableElement>
                 |  <TableElements> <OptComma> <TableElement>

<TableContents> ::= <TableElements> <OptComma>
                     |
