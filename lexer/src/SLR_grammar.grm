Comment Line = '#'

"Start Symbol" = <TableContents>

! -------------------------------------------------
! Character Sets
! -------------------------------------------------

{Escaped String Chars} = {Printable} + {Whitespace} - ["\]
{Newline} = {CR} + {LF}
                      
{Raw String Chars} = {Printable} + {Whitespace}
{Raw String 0 Chars} = {Printable} + {Whitespace} - ["]

{Naked String Middle} = {Printable} - {Whitespace} - [#='['']'{}$",~] + [' ']
{Naked String Start} = {Naked String Middle} - [' ']

! -------------------------------------------------
! Terminals
! -------------------------------------------------

NakedString = {Naked String Start} {Naked String Middle}*

EscapedString = '"' ( {Escaped String Chars} | '\' {Printable} | '\' {Newline} )* '"'

RawString0 = 'r"' {Raw String 0 Chars}* '"'
RawString1 = 'r{"' {Raw String Chars}* '"}'
RawString2 = 'r{{"' {Raw String Chars}* '"}}'

! -------------------------------------------------
! Rules
! -------------------------------------------------

! Expressions
<String> ::= NakedString | EscapedString | RawString0 | RawString1 | RawString2
          
<IndexExpr> ::= <String>
             |  <IndexExpr> '[' <String> ']'
             | 'root'
             | 'import'

<Expansion> ::= '$' <IndexExpr>

<StringSource> ::= <String>
                |  <Expansion>

<StringExpr> ::= <StringSource>
             |   <StringExpr> '~' <StringSource>

<NoDeleteExpr> ::= <StringExpr>        
                |  '{' <TableContents> '}'
                |  '[' <ArrayContents> ']'

<Expr> ::= <NoDeleteExpr> |  '~'

! Statements
<Assignment> ::= <IndexExpr> '=' <Expr>

<ArrayElement> ::= <Assignment> | <NoDeleteExpr>
             
<ArrayElements> ::= <ArrayElement>
                 |  <ArrayElements> ',' <ArrayElement>

<ArrayContents> ::= <ArrayElements>
                     |

<TableElement> ::= <Assignment> | <Expansion>

<TableElements> ::= <TableElement>
                 |  <TableElements> <TableElement>

<TableContents> ::= <TableElements>
                     |
